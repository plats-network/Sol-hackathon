import java.text.SimpleDateFormat
import java.util.Date

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def versionProperties = new Properties()
def versionPropertiesFile = rootProject.file('version.properties')
if (versionPropertiesFile.exists()) {
    versionPropertiesFile.withReader('UTF-8') { reader ->
        versionProperties.load(reader)
    }
}

def flutterVersionCode = versionProperties.getProperty('versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '26'
}

def flutterVersionName = versionProperties.getProperty('versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0.0'
}

static def getCurrentTimestamp() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("dd-MM-yyyy")
    return df.format(today)
}

def changeApkName = { variant ->
    variant.outputs.each { output ->
        def startTime = getCurrentTimestamp()
        def apk = output.outputFile
        def newName = "PlatsNetwork_" + startTime + "_" + flutterVersionCode + ".apk"
        if (!output.zipAlign) {
            newName = newName.replace(".apk", "-unaligned.apk")
        }
        output.outputFileName = new File(apk.parentFile, newName).getName();

        println 'INFO: Set outputFile to ' + output.outputFile + " for [" + output.name + "]"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def keystoreProperties = new Properties()
   def keystorePropertiesFile = rootProject.file('key.properties')
   if (keystorePropertiesFile.exists()) {
       keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
android {
    compileSdkVersion 33
    ndkVersion flutter.ndkVersion

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "network.plats.action"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        multiDexEnabled true
        minSdkVersion 21
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
       release {
           keyAlias keystoreProperties['keyAlias']
           keyPassword keystoreProperties['keyPassword']
           storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
           storePassword keystoreProperties['storePassword']
       }
   }
   buildTypes {
       release {
           signingConfig signingConfigs.release
       }
   }

    applicationVariants.all { variant ->
        changeApkName(variant)
    }

    tasks.whenTaskAdded { task ->
        if (task.getName().contains('compileFlutterBuildRelease')) {
            // Increase version code
            println 'INFO: Increase version code'
            versionProperties['versionName'] = flutterVersionName
            versionProperties['versionCode'] = (flutterVersionCode.toInteger() + 1).toString()
            versionProperties.store(versionPropertiesFile.newWriter(), null)
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation platform('com.google.firebase:firebase-bom:30.2.0')
    implementation 'com.facebook.android:facebook-login:14.1.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation "androidx.window:window:1.0.0-rc01"
    implementation "androidx.window:window-java:1.0.0-rc01"
}
